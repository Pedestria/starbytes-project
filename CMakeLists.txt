
cmake_minimum_required (VERSION 3.18)
set(CMAKE_EXPORT_COMPILE_COMMANDS 1)
# cmake_policy(SET CMP0079 NEW)

option(USE_VCPKG "Use vcpkg manager!" OFF)
option(USE_VCPKG_AS_TOOLCHAIN "Use vcpkg cmake toolchain file" OFF)
set(ENABLE_STARBYTES_LSP TRUE)


set(STARBYTES_PROJECT_VERSION "0.9.0")
message("Starbytes Project Version " ${STARBYTES_PROJECT_VERSION})

macro(find_vcpkg USE_TOOLCHAIN)
	message(STATUS "Locating vcpkg executable")
	find_program(VCPKG_ROOT "vcpkg" ONLY_CMAKE_FIND_ROOT_PATH)
	if(NOT EXISTS ${VCPKG_ROOT})
		message(STATUS "Locating vcpkg executable - not found")
	else()
		message(STATUS "Locating vcpkg executable - found")
		get_filename_component(VCPKG_DIR ${VCPKG_ROOT} DIRECTORY)
		message(STATUS "Locating vcpkg ports")
		if(IS_DIRECTORY ${VCPKG_DIR}/ports)
			message(STATUS "Locating vcpkg ports - found")
			if(${USE_TOOLCHAIN})
				message(STATUS "Locating vcpkg toolchain file")
				set(VCPKG_TOOLCHAIN_FILE_DIR ${VCPKG_DIR}/scripts/buildsystems)
				if(EXISTS ${VCPKG_TOOLCHAIN_FILE_DIR}/vcpkg.cmake)
					message(STATUS "Locating vcpkg toolchain file - found")
					message(STATUS "Loading toolchain")
					include(${VCPKG_TOOLCHAIN_FILE_DIR}/vcpkg.cmake)
				else()
					message(STATUS "Locating vcpkg toolchain file - not found")
				endif()
			endif()
		else()
			message(STATUS "Locating vcpkg ports - not found")
			message(FATAL_ERROR "vcpkg executable is provided but there are NO ports found in its directory!")
		endif()
	endif()
endmacro()

if(${USE_VCPKG})
	if(${USE_VCPKG_AS_TOOLCHAIN})
		find_vcpkg(TRUE)
	else()
		find_vcpkg(FALSE)
	endif()
endif()


# set(STARBYTES_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/modules)
# list(APPEND CMAKE_MODULE_PATH ${STARBYTES_MODULE_PATH})


# if(UNIX)
# 	include(AddClangCXXLib)
# endif()


# set(CMAKE_C_STANDARD 11)
# set(CMAKE_C_STANDARD_REQUIRED YES)
# set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
# set(CMAKE_LINK_FLAGS "${CMAKE_LINK_FLAGS} -frtti")
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED YES)


project ("Starbytes" C CXX)

enable_testing()


set(RAPIDJSON_INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}/deps/rapidjson/include")

include_directories(${RAPIDJSON_INCLUDE_DIRS})

add_compile_definitions(STARBYTES_VERSION=${STARBYTES_PROJECT_VERSION})


include(./Starbytes.cmake)

if(XCODE)
    set_property(GLOBAL PROPERTY USE_FOLDERS ON)
endif()

if(APPLE)
	message("macOS: ${CMAKE_OSX_SYSROOT}")
endif()

include(CheckIncludeFile)
check_include_file(unistd.h UNISTD_H_FOUND)
check_include_file(filesystem FILESYSTEM_H_FOUND)
check_include_file(sys/wait.h SYS_WAIT_H_FOUND)
if(UNISTD_H_FOUND)
    add_header_macro("unistd.h" ${UNISTD_H_FOUND})
endif()
if(FILESYSTEM_H_FOUND)
    add_header_macro("filesystem" ${FILESYSTEM_H_FOUND})
endif()
if(SYS_WAIT_H_FOUND)
	add_header_macro("sys_wait" ${SYS_WAIT_H_FOUND})
endif()

install(DIRECTORY "include/" DESTINATION "${CMAKE_INSTALL_PREFIX}/include" FILES_MATCHING PATTERN "*.h")

set(STARBYTES_INCLUDE_DIR ${CMAKE_SOURCE_DIR}/include/starbytes)

file(GLOB STARBYTES_BASE_SRCS ${CMAKE_SOURCE_DIR}/src/base/*.cpp)
file(GLOB STARBYTES_COMPILER_SRCS ${CMAKE_SOURCE_DIR}/src/compiler/*.cpp)
file(GLOB STARBYTES_RUNTIME_SRCS ${CMAKE_SOURCE_DIR}/src/runtime/*.cpp ${CMAKE_SOURCE_DIR}/src/runtime/*.c)

file(GLOB STARBYTES_BASE_HEADERS ${STARBYTES_INCLUDE_DIR}/base/*.h)
file(GLOB STARBYTES_COMPILER_HEADERS ${STARBYTES_INCLUDE_DIR}/compiler/*.h)
file(GLOB STARBYTES_RUNTIME_HEADERS ${STARBYTES_INCLUDE_DIR}/runtime/*.h)

if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/deps/pcre2/CMakeLists.txt")
    set(PCRE2_BUILD_PCRE2_8 ON CACHE BOOL "" FORCE)
    set(PCRE2_BUILD_PCRE2_16 OFF CACHE BOOL "" FORCE)
    set(PCRE2_BUILD_PCRE2_32 OFF CACHE BOOL "" FORCE)
    set(PCRE2_BUILD_PCRE2GREP OFF CACHE BOOL "" FORCE)
    set(PCRE2_BUILD_TESTS OFF CACHE BOOL "" FORCE)
    add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/deps/pcre2" EXCLUDE_FROM_ALL)
    set(STARBYTES_PCRE2_TARGET pcre2-8)
else()
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(PCRE2 REQUIRED IMPORTED_TARGET libpcre2-8)
    set(STARBYTES_PCRE2_TARGET PkgConfig::PCRE2)
endif()

set(STARBYTES_ASIO_INCLUDE_DIRS)
if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/deps/asio/asio/include/asio.hpp")
    list(APPEND STARBYTES_ASIO_INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}/deps/asio/asio/include")
endif()

set(STARBYTES_CURL_TARGET "")
set(STARBYTES_CURL_INCLUDE_DIRS "")
if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/deps/libcurl/CMakeLists.txt")
    set(BUILD_CURL_EXE OFF CACHE BOOL "" FORCE)
    set(BUILD_STATIC_LIBS ON CACHE BOOL "" FORCE)
    set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)
    set(CURL_DISABLE_INSTALL ON CACHE BOOL "" FORCE)
    set(CURL_BUILD_TESTING OFF CACHE BOOL "" FORCE)
    set(BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
    set(CURL_USE_LIBPSL OFF CACHE BOOL "" FORCE)
    set(CURL_USE_LIBSSH2 OFF CACHE BOOL "" FORCE)
    set(CURL_USE_LIBSSH OFF CACHE BOOL "" FORCE)
    set(CURL_USE_GSSAPI OFF CACHE BOOL "" FORCE)
    set(CURL_USE_GSASL OFF CACHE BOOL "" FORCE)
    set(CURL_ENABLE_SSL OFF CACHE BOOL "" FORCE)
    set(CURL_USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(CURL_ZLIB OFF CACHE BOOL "" FORCE)
    set(CURL_BROTLI OFF CACHE BOOL "" FORCE)
    set(CURL_ZSTD OFF CACHE BOOL "" FORCE)
    add_subdirectory("${CMAKE_CURRENT_SOURCE_DIR}/deps/libcurl" EXCLUDE_FROM_ALL)
    if(TARGET CURL::libcurl)
        set(STARBYTES_CURL_TARGET CURL::libcurl)
    elseif(TARGET libcurl)
        set(STARBYTES_CURL_TARGET libcurl)
    elseif(TARGET libcurl_static)
        set(STARBYTES_CURL_TARGET libcurl_static)
    endif()
    if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/deps/libcurl/include")
        set(STARBYTES_CURL_INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}/deps/libcurl/include")
    endif()
else()
    find_package(CURL QUIET)
    if(CURL_FOUND)
        if(TARGET CURL::libcurl)
            set(STARBYTES_CURL_TARGET CURL::libcurl)
        endif()
        if(CURL_INCLUDE_DIRS)
            set(STARBYTES_CURL_INCLUDE_DIRS "${CURL_INCLUDE_DIRS}")
        endif()
    endif()
endif()

add_starbytes_lib(LIB_NAME "starbytesBase" SOURCE_FILES ${STARBYTES_BASE_SRCS} HEADER_FILES ${STARBYTES_BASE_HEADERS})
add_starbytes_lib(LIB_NAME "starbytesCompiler" SOURCE_FILES ${STARBYTES_COMPILER_SRCS} HEADER_FILES ${STARBYTES_COMPILER_HEADERS} LIBS_TO_LINK "starbytesBase")
add_starbytes_lib(LIB_NAME "starbytesRuntime" SOURCE_FILES ${STARBYTES_RUNTIME_SRCS} HEADER_FILES ${STARBYTES_RUNTIME_HEADERS} LIBS_TO_LINK "starbytesBase;starbytesCompiler")
target_link_libraries(starbytesRuntime PRIVATE ${STARBYTES_PCRE2_TARGET})


add_subdirectory("tools")
add_subdirectory("tests")
add_subdirectory("stdlib")
# add_subdirectory("ide")
# TODO : Get Starbytes Compilation to work!
# add_subdirectory("starbytes-modules")

# if(ENABLE_STARBYTES_LSP)
# 	add_subdirectory(../starbytes-lsp "lsp")
# endif()

#target_link_libraries(CPlusPlus PRIVATE nlohmann_json nlohmann_json::nlohmann_json)
# TODO: Add tests and install targets if needed.
